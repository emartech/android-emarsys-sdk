ext.modules = [':core-api', ':core', ':mobile-engage-api', ':mobile-engage', ':predict-api', ':predict', ':emarsys', ':emarsys-sdk']

buildscript {
    repositories {
        jcenter()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0'
        classpath 'digital.wup:android-maven-publish:3.6.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$Versions.kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$Versions.kotlin_version"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.33.0"
        classpath "com.vanniktech:gradle-dependency-graph-generator-plugin:0.5.0"
        classpath "android.arch.navigation:navigation-safe-args-gradle-plugin:1.0.0"
    }
}

apply from: 'gradle/dev.gradle'
apply from: 'gradle/version.gradle'
apply from: 'gradle/common.gradle'
apply from: 'gradle/release.gradle'
apply plugin: "com.github.ben-manes.versions"
apply plugin: "com.vanniktech.dependency.graph.generator"

task clean(type: Delete) {
    delete rootProject.buildDir
}

dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            if (!(selection.candidate.version ==~ /\d+\.\d+(\.\d+)?/)) {
                selection.reject('Not release candidate!')
            }
        }
    }
}