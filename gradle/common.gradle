allprojects {
    version = gitVersionName
    group = 'com.emarsys'
}

modules.each {
    project(it) {
        apply plugin: 'com.android.library'
        apply plugin: 'kotlin-android'
        apply plugin: "kotlin-allopen"
        apply plugin: 'kotlin-kapt'

        android {
            buildToolsVersion Config.buildToolsVersion
            compileSdkVersion Config.targetApiLevel
            defaultConfig {
                minSdkVersion Config.minApiLevel
                targetSdkVersion Config.targetApiLevel
                buildConfigField "int", "VERSION_CODE", "$gitVersionCode"
                buildConfigField "String", "VERSION_NAME", "\"$gitVersionName\""
                testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
            }
            lintOptions {
                checkDependencies true
            }
            compileOptions {
                coreLibraryDesugaringEnabled true
            }

            kotlin {
                jvmToolchain(17)
            }

            dependencies {
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.2.0'
            }
            buildTypes {
                release {
                    minifyEnabled false
                    proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                }
                debug {
                    multiDexKeepProguard file("$rootDir/proguard-multidex-rules.pro")
                    multiDexEnabled true
                }
            }

            packagingOptions {
                exclude 'README.txt'
                exclude 'META-INF/LICENSE.md'
                exclude 'META-INF/LICENSE-notice.md'
            }
            useLibrary 'android.test.runner'

            useLibrary 'android.test.base'
            useLibrary 'android.test.mock'
        }
        allOpen {
            annotation("com.emarsys.core.Mockable")
        }
    }
}