apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'androidx.navigation.safeargs'

group = 'com.emarsys.sample'

android {
    namespace = "com.emarsys.sample"
    buildToolsVersion Config.buildToolsVersion
    compileSdkVersion Config.targetApiLevel
    defaultConfig {
        applicationId "com.emarsys.sample"
        minSdkVersion 24
        targetSdkVersion 33
        multiDexEnabled true
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "com.emarsys.sample.testutils.SampleAppTestRunner"
        resValue "string", "sdk_version", "${gitVersionName}"

        def clientId = null
        def localConfigPropertiesFile = rootProject.file('localConfig.properties')
        def localConfigProperties = new Properties()
        if (localConfigPropertiesFile.exists()) {
            localConfigProperties.load(new FileInputStream(localConfigPropertiesFile))
            clientId = localConfigProperties.getProperty("googleOAuthServerClientId")
        }

        buildConfigField "String", "GOOGLE_OAUTH_SERVER_CLIENT_ID", "\"${System.env.GOOGLE_OAUTH_SERVER_CLIENT_ID ?: clientId}\""
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.4.4-dev-k1.8.20-f6ae19e64ff'
        kotlinCompilerVersion '1.8.0'
    }

    if (project.devConfig.releaseSampleApp || System.env.DEVELOPMENT_MODE) {
        signingConfigs {
            release {
                storePassword System.env.RELEASE_STORE_PASSWORD ?: project.devConfig.releaseStorePassword
                keyAlias System.env.RELEASE_KEY_ALIAS ?: project.devConfig.releaseKeyAlias
                keyPassword System.env.RELEASE_KEY_PASSWORD ?: project.devConfig.releaseKeyPassword
                storeFile file(System.env.RELEASE_STORE_FILE ?: project.devConfig.releaseStoreFile)
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    buildTypes {
        debug {
            multiDexKeepProguard file("proguard-multidex-rules.pro")
            multiDexEnabled true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
        }
    }
}

buildscript {
    repositories {
        mavenLocal()
        google()
        mavenCentral()
        maven { url 'https://developer.huawei.com/repo/' }
    }
    dependencies {
        classpath 'com.google.gms:google-services:4.3.15'
        classpath 'com.huawei.agconnect:agcp:1.8.1.300'
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    if (project.devConfig.useLocalDependency || System.env.DEVELOPMENT_MODE) {
        implementation project(':emarsys-sdk')
        implementation project(':emarsys-firebase')
        implementation project(':emarsys-huawei')
    } else {
        implementation 'com.emarsys:emarsys-sdk:+'
        implementation 'com.emarsys:emarsys-firebase:+'
        implementation 'com.emarsys:emarsys-huawei:+'
    }
    // Getting a "Could not find firebase-core" error? Make sure you have
    // the latest Google Repository in the Android SDK manager

    implementation "org.jetbrains.kotlin:kotlin-reflect:1.8.20"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.8.20"
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.google.android.gms:play-services-auth:20.5.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'io.coil-kt:coil:2.3.0'

    implementation "androidx.core:core-ktx:1.9.0"
    implementation "com.google.android.material:material:1.8.0"
    implementation "androidx.compose.ui:ui:1.4.0"
    implementation "androidx.compose.material:material:1.4.0"
    implementation "androidx.compose.ui:ui-tooling-preview:1.4.0"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation "androidx.compose.material:material-icons-extended:1.4.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.6.1"
    implementation "androidx.activity:activity-compose:1.7.0"
    implementation "io.coil-kt:coil-compose:2.3.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "com.chibatching.kotpref:kotpref:2.13.2"
    implementation "com.google.firebase:firebase-common-ktx:20.3.2"
    implementation "com.google.firebase:firebase-messaging-ktx:23.1.2"
    implementation "com.google.android.gms:play-services-auth:20.5.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.30.1"
    implementation "com.fasterxml.jackson.module:jackson-module-kotlin:2.14.2"

    debugImplementation "androidx.compose.ui:ui-tooling:1.4.0"

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation "androidx.test.ext:junit:1.1.5"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.5.1"
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:1.4.0"
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation('com.adevinta.android:barista:4.3.0') {
        exclude group: 'org.jetbrains.kotlin'
    }
    androidTestImplementation 'androidx.test.espresso:espresso-web:3.5.1'
    androidTestImplementation 'io.kotlintest:kotlintest-runner-junit4:3.4.2'
}


task startApp(type: Exec, dependsOn: 'installDebug') {
    commandLine 'adb', 'shell', 'am', 'start', '-n', 'com.emarsys.sample/.main.MainActivity'
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.huawei.agconnect'
