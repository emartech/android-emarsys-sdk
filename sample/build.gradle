plugins {
    alias(libs.plugins.androidApplication)
    alias(libs.plugins.kotlin)
    alias(libs.plugins.kapt)
    alias(libs.plugins.navigationSafeArgs)
    alias(libs.plugins.kotlinAllOpen)
}

group = 'com.emarsys.sample'

android {
    namespace = "com.emarsys.sample"
    compileSdkVersion 34
    defaultConfig {
        applicationId "com.emarsys.sample"
        minSdkVersion 24
        targetSdkVersion 34
        multiDexEnabled true
        versionCode gitVersionCode
        versionName gitVersionName
        testInstrumentationRunner "com.emarsys.sample.testutils.SampleAppTestRunner"
        resValue "string", "sdk_version", "${gitVersionName}"

        def clientId = null
        def localConfigPropertiesFile = rootProject.file('localConfig.properties')
        def localConfigProperties = new Properties()
        if (localConfigPropertiesFile.exists()) {
            localConfigProperties.load(new FileInputStream(localConfigPropertiesFile))
            clientId = localConfigProperties.getProperty("googleOAuthServerClientId")
        }

        buildConfigField "String", "GOOGLE_OAUTH_SERVER_CLIENT_ID", "\"${System.env.GOOGLE_OAUTH_SERVER_CLIENT_ID ?: clientId}\""
    }

    buildFeatures {
        compose true
    }

    kotlin {
        jvmToolchain(17)
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.1'
        kotlinCompilerVersion '1.9.0'
    }

    if (project.devConfig.releaseSampleApp || System.env.DEVELOPMENT_MODE) {
        signingConfigs {
            release {
                storePassword System.env.RELEASE_STORE_PASSWORD ?: project.devConfig.releaseStorePassword
                keyAlias System.env.RELEASE_KEY_ALIAS ?: project.devConfig.releaseKeyAlias
                keyPassword System.env.RELEASE_KEY_PASSWORD ?: project.devConfig.releaseKeyPassword
                storeFile file(System.env.RELEASE_STORE_FILE ?: project.devConfig.releaseStoreFile)
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    buildTypes {
        debug {
            multiDexKeepProguard file("proguard-multidex-rules.pro")
            multiDexEnabled true
        }
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/LICENSE.md', 'META-INF/LICENSE-notice.md']
        }
    }
}

dependencies {
    if (project.devConfig.useLocalDependency == true || System.env.DEVELOPMENT_MODE == true) {
        implementation project(':emarsys-sdk')
        implementation project(':emarsys-firebase')
        implementation project(':emarsys-huawei')
    } else {
        implementation 'com.emarsys:emarsys-sdk:+'
        implementation 'com.emarsys:emarsys-firebase:+'
        implementation 'com.emarsys:emarsys-huawei:+'
    }
    // Getting a "Could not find firebase-core" error? Make sure you have
    // the latest Google Repository in the Android SDK manager

    implementation "org.jetbrains.kotlin:kotlin-reflect:1.9.21"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.21"
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.android.support:cardview-v7:28.0.0'
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'io.coil-kt:coil:2.5.0'

    implementation "androidx.core:core-ktx:1.12.0"
    implementation "com.google.android.material:material:1.11.0"
    implementation "androidx.compose.ui:ui:1.6.0"
    implementation "androidx.compose.material:material:1.6.0"
    implementation "androidx.compose.ui:ui-tooling-preview:1.6.0"
    implementation "androidx.navigation:navigation-compose:2.7.6"
    implementation "androidx.compose.material:material-icons-extended:1.6.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.7.0"
    implementation "androidx.activity:activity-compose:1.8.2"
    implementation "io.coil-kt:coil-compose:2.5.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    implementation "com.chibatching.kotpref:kotpref:2.13.2"
    implementation "com.google.firebase:firebase-common-ktx:20.4.2"
    implementation "com.google.firebase:firebase-messaging-ktx:23.4.0"
    implementation "com.google.android.gms:play-services-auth:20.7.0"
    implementation "com.google.accompanist:accompanist-swiperefresh:0.34.0"
    implementation "com.google.code.gson:gson:2.10.1"
    debugImplementation "androidx.compose.ui:ui-tooling:1.6.0"
}
